' Gambas class file

Public ControladorAplicacion As Controlador

Public TmpAlumno As AlumnoVO

Private resultadoCurso As Result

Public Sub Form_Open()
  
  Dim mirela As New RelacionDAO
  'formateo el gridvies, segun el resultado de una consulta...
  GridViewRelacionCursos = mirela.GridResultanteSQL(ControladorAplicacion.ContenidoRelacionesIdAlumnos(TmpAlumno.idAlumno), GridViewRelacionCursos)
  refrescarDatosCursos()
  RevisarAnchos(GridViewRelacionCursos)
  Me.Title = "Expedientes de Alumnos"

End

Public Sub form_Show()
  
  Dim mirela As New RelacionDAO
  
  mirela.GridResultanteSQL(ControladorAplicacion.ContenidoRelacionesIdAlumnos(TmpAlumno.idAlumno), GridViewRelacionCursos)
  
  TextBoxNombreAlumno.text = TmpAlumno.Nombre
  TextBoxApellidosAlumno.text = TmpAlumno.Apellido
  TextBoxDocumento.text = TmpAlumno.Documento
  refrescarDatosCursos()
  RevisarAnchos(GridViewRelacionCursos)
  
End

Public Sub ToolButtonAceptar_Click()
  
  Me.Close()
  
End

Public Sub ToolButtonAnnadirAlumnoCurso_Click()
  
  Dim rela As New RelacionVO
  Dim cursoelegido As New ClaseDato
  
  ControladorAplicacion.EligeCurso.cursoelegido = cursoelegido
  
  ControladorAplicacion.EligeCurso.showmodal()
  If cursoelegido.tag <> "" Then 
    'añadir relacion sabiendo idcurso,idalumno
    rela.idAlumno = TmpAlumno.idAlumno
    rela.idCurso = cursoelegido.tag
    
    rela.Observacion = InputBox("Observacion:", "Introduzca una observacion")
    If ControladorAplicacion.anadirRelacion(rela) Then 
      Message.Info("Curso asignado al alumno")
      refrescarDatosCursos()
    Endif
    
  Endif
  
End

'-----------------------------------------------------------------

Public Sub RefrescarDatosCursos()
  
  resultadoCurso = ControladorAplicacion.ContenidoRelacionesIdAlumnos(TmpAlumno.idAlumno)
  
  GridViewRelacionCursos.Rows.count = 0 'lo ponemos a cero el numero de filas
  GridViewRelacionCursos.Rows.count = resultadoCurso.count 'actualizamso al resultado de la consulta
  
End

Public Sub GridViewRelacionCursos_Data(row As Integer, column As Integer)
  ' este evento se encarga de dibujar los datos en el gridviews, para que aparezcan en la pantalla.
  
  Try resultadoCurso.MoveTo(row)
  If Error Then Return 
  GridViewRelacionCursos.Data.text = Str(resultadoCurso[GridViewRelacionCursos.Columns[column].text])
  
  ' colorea la fila, según sea par o impar
  If row = GridViewRelacionCursos.row Then 
    'cursor donde esta situado en el gridview
    GridViewRelacionCursos.Data.Background = Color.Orange
  Else
    If row Mod 2 = 0 Then GridViewRelacionCursos.Data.Background = Color.Cyan
  Endif
  
End

Public Sub ToolButtonBorrarAlumno_Click()
  
  If GridViewRelacionCursos.row = -1 Then 
    Message.Info("Seleccione un curso")
  Else
    
    If ControladorAplicacion.borrarAlumnoDeCurso(Val(GridViewRelacionCursos[GridViewRelacionCursos.row, 3].Text)) Then 
      Message.Info("Alumno borrado del curso")
      refrescarDatosCursos()
    Endif
    
  Endif
  
End

Public Sub ToolButtonEditarObservacion_Click()
  
  Dim rela As New RelacionVO
  Dim textoObservacion As String
  
  If GridViewRelacionCursos.row = -1 Then 
    Message.Info("Seleccione un curso")
  Else
    
    textoObservacion = InputBox("Introduzca Observación:", "Observación del Aluno en el Curso " & GridViewRelacionCursos[GridViewRelacionCursos.row, 1].Text, GridViewRelacionCursos[GridViewRelacionCursos.row, 2].Text)
    If textoObservacion <> "" Then 
      'modificar registro de observacion
      rela.idAlumno = Val(GridViewRelacionCursos[GridViewRelacionCursos.row, 4].Text)
      rela.idCurso = Val(GridViewRelacionCursos[GridViewRelacionCursos.row, 3].Text)
      rela.idRelacion = Val(GridViewRelacionCursos[GridViewRelacionCursos.row, 0].Text)
      rela.Observacion = textoObservacion
      
      If ControladorAplicacion.modificarIdRelacion(rela) Then 
        refrescarDatosCursos()
        Message.Info("Observación modificada")
      Else
        Message.Error("No se ha podido modificar la observacion")
      Endif
    Endif
    
  Endif
  
End

'para el tema de dimensionado del formulario
Public Sub Form_Resize()
  
  GridViewRelacionCursos.w = Me.w - GridViewRelacionCursos.x - 20
  
  GridViewRelacionCursos.h = Me.H - GridViewRelacionCursos.y - 10
  
End

'reviso anchos de las columnas
Public Sub RevisarAnchos(rejilla As GridView)
  
  With rejilla 
    .Columns[0].width = 0
    .Columns[2].width = 1000
    .Columns[3].width = 0
    .Columns[4].width = 0
    
  End With
  
End
